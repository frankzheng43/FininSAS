option nonotes nolabel;
libname aa "D:\The Application of SAS in Financial Research\CH10\data";
data a;
   set aa.price;
      if mv=. then delete;
      if ret=. then delete;
      ret=1+0.01*ret;
      month=y*100+m;
      keep  code ret mv month;
run;
%macro JT(file, firm,time,J,k,group);
   proc sort data=&file;by &firm &time;
   run;

   data &file;
      set &file;
         %do i=1 %to &j;
            r&i=lag&i(ret);
            j&i=(geomean(of r1-r&j)-1)*100;
            if &firm^=lag&i(&firm) then j&i=.;
         %end;
   run;
   proc sort data=&file out=&file(drop=r1-r&j) ;by &time ;
   run;
   proc rank data=&file out=rank(drop=j1-j&j) groups=&group;
      var j&j;
      ranks rank;
      by &time ;
   run;

   proc sort data=rank;by &firm descending &time;
   data rank;
      set rank;
         ret=(ret-1)*100;
         ret1=lag(ret);
         if &firm^=lag(&firm) then ret1=.;
   run;
   proc sort data=rank;by &firm &time;
   run;
   data rank;
      set rank;
         mv1=mv;
         rank=rank+1;
         mv=lag(mv);
         if &firm^=lag(&firm) then mv=.;
         if rank=. then delete;
   run;
   proc sort data=rank ;by rank &time;
   run;
   proc means noprint data=rank;
      var ret ret1;
      by rank &time;
      output out=ew(drop=_type_ _freq_) mean=ewr ewr1;
   run;
   proc means noprint data=rank;
      var ret ;
      weight mv;
      by rank &time;
      output out=vw(drop=_type_ _freq_) mean=vwr;
   run;
   proc means noprint data=rank;
      var ret1 ;
      weight mv1;
      by rank &time;
      output out=vw1(drop=_type_ _freq_) mean=vwr1;
   run;
   data bb;
      merge ew vw vw1;by rank &time;
         j=&j;
         k=&k;
   run;
   proc sort data=rank;by &firm &time;
   run;
   %if &K>1 %then %do; 
      %do i=1 %to &k-1;
         data b;
            set rank;
               rank=lag&i(rank);
               if &firm^=lag&i(&firm) then delete;
         run;
         proc sort data=b;by rank &time;
         run;
         proc sort data=b ;by rank &time;
         run;
         proc means noprint data=b;
            var ret ret1;
            by rank &time;
            output out=ew(drop=_type_ _freq_) mean=ewr ewr1;
         run;
         proc means noprint data=b;
            var ret ;
            weight mv;
            by rank &time;
            output out=vw(drop=_type_ _freq_) mean=vwr;
         run;
         proc means noprint data=b;
            var ret1 ;
            weight mv1;
            by rank &time;
            output out=vw1(drop=_type_ _freq_) mean=vwr1;
         run;
         data bbb;
            merge ew vw vw1;
               j=&j;
               k=&k;
         run;
         proc append base=bb data=bbb;
         quit;
      %end;
   %end;
   proc sort data=bb;by j k  &time rank;
   run;
   proc means noprint data=bb;
      var ewr ewr1 vwr vwr1;
      by j k  month rank;
      output out=mean(drop=_type_ _freq_) mean=ew ew1 vw vw1 n=n;
   run;
   data mean;
      set mean;
         portfolio="p"|| left(rank||"");
         if k=n then output;
         drop n;
   run;
   proc append base=full_return data=mean;
   quit;
   proc transpose data=mean out=mean;
      var ew ew1 vw vw1;
      id portfolio;
      by j k &time;
   run;
   data mean;
      set mean;
         mom=p&group-p1;
         return=_name_;
   run;
   proc sort data=mean;by j k return;
   run;
   proc means noprint data=mean;
      var p1-p&group mom;
      by j  k return ;
   output out=mean(drop=_type_ _freq_);
   run;
   proc transpose data=mean out=mean;
      id _stat_;
      by j k return;
   run;
   proc append base=JT data=mean;
   quit;
%mend;
proc datasets ;
delete jt full_return;;
quit;

%JT(a,code, month, 3,3,10);
%JT(a,code, month, 3,6,10);
%JT(a,code, month, 3,9,10);
%JT(a,code, month, 3,12,10);
%JT(a,code, month, 6,3,10);
%JT(a,code, month, 6,6,10);
%JT(a,code, month, 6,9,10);
%JT(a,code, month, 6,12,10);
%JT(a,code, month, 9,3,10);
%JT(a,code, month, 9,6,10);
%JT(a,code, month, 9,9,10);
%JT(a,code, month, 9,12,10);
%JT(a,code, month, 12,3,10);
%JT(a,code, month, 12,6,10);
%JT(a,code, month, 12,9,10);
%JT(a,code, month, 12,12,10);

%let group=10;
data b;
set jt;
t=mean/(std/(n)**0.5);
tvalue="("||left(round(t,0.01)||")");
if abs(t)>2.58 then r=round(mean, 10**-3)||"***";
else if 2.58>=abs(t)>1.96 then r=round(mean, 10**-3)||"**";
else if 1.96>=abs(t)>1.65 then r=round(mean, 10**-3)||"*";
else r=round(mean, 10**-3)||"";
run;
proc sort data=b;by return j k;
run;
proc transpose data=b out=mean(drop=_name_);
var r;
by return j k;
run;
proc transpose data=b out=tvalue(drop=_name_);
var tvalue;
by return j k;
run;
proc transpose data=mean out=mean;
var p1-p&group mom;
by return j;
id k;
run;
proc transpose data=tvalue out=tvalue;
var p1-p&group mom;
by return j;
id k;
run;
data mean;
set mean;
retain t 0;
t=t+1;
type=1;
run;
data tvalue;
set tvalue;
retain t 0;
t=t+1;
type=2;
run;
data final;
set mean tvalue;by t type;
if type=2 then _name_="";
drop t type;
run;
