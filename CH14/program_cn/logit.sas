
%macro logit(file,group,bit,outfile);
data Ian_reg;
   set &file;
   ian=1;
run;
proc sort data=ian_reg;by ian &group;
run;
proc logistic data=ian_reg desc ;
   model %model /maxiter=100 rsquare;
   by ian &group;
   ods output ParameterEstimates=esti  rsquare=rsq nobs=n; 
quit;
data esti;
   set esti;
      retain t 0;
         t=t+1;
      if probchisq<0.01 then sig=round(estimate,10**-&bit)||"***";
      else if 0.01<probchisq=<0.05 then sig=round(estimate,10**-&bit)||"**";
      else if 0.05<probchisq=<0.1 then sig=round(estimate,10**-&bit)||"*";
      else sig=round(estimate,10**-&bit)||"";
      tv="(" || left (  round(estimate/stderr,0.01)||")");
      tv2=(estimate/stderr)**2;
run;
data a1;
   set esti;
      model=left(sig);
      type=1;
      keep model t type variable &group;
run;
data a2;
   set esti;
      model=tv;
      type=2;
keep model t type variable &group;
run;
data rsq;
   set rsq;
      model=left(round(nvalue1*100,0.01)||"%");
      t=100;
      type=1;
      variable="R2";
      keep model t type variable &group;
run;
data n;
   set n;
      model=left(NObsUsed||"");
      t=1000;
      type=1;
      variable="N";
      if _n_=2 then output;
      keep model t type variable &group;
run;
data final;
   set a1 a2 rsq n;
run;
proc sort data=final out=&outfile(keep=&group t type model variable);by &group t type;
run;
proc delete data=a1 a2 rsq final n esti ian_reg;
quit;
%mend;
%macro logit_file(group,n,file);
   %do i=1 %to &n;
      data logit&i;
	     set logit&i;
		    model&i=model;
			%if &i^=1 %then %do;
			   drop t;
			%end;
	  run;
      proc sort data=logit&i;by &group variable type;
	  run;
	  %if &i=1 %then %do;
	     data ian;
		    set logit&i;
		 run;
		 proc sort data=ian;by &group variable type;
		 run;
	 %end;
	 %else %do;
	    data ian;
		   merge ian logit&i;by &group variable type;
		run;
	 %end;
     proc delete data=logit&i;
	 quit;
    %end;
	 data ian;
	    set ian;
	       if type=2 then variable='';
	       drop model model1;
	 run;
   proc sort data=ian out=&file(drop=t type);by &group t type;
   run;
   proc delete data=ian;
   quit;
%mend;
