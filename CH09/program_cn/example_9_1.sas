option nolabel;
libname aa "D:\The Application of SAS in Financial Research\SAS data\monthly price";
data a;
   set aa.price;
      if m=12  and  y>2000 then  output;
run;
proc sort data=a;by y;
run;
proc rank data=a out=a groups=2;
   var mv;
   ranks size;
   by y;
run;

proc ttest data=a;
   var volum value ret turn pb;
   class size;
   ods output Statistics=t;
run;
data t;
   set t;
      order=int((_n_-1)/3);
      tv="("||left(round(mean/stderr,0.01)||")");
      if abs(mean/stderr)>2.58 then m=round(mean,0.001)||"***";
      else if abs(mean/stderr)>1.96 then m=round(mean,0.001)||"**";
      else if abs(mean/stderr)>1.65 then m=round(mean,0.001)||"*";
      else m=round(mean,0.001)||"";
      if mod(_n_,3)=1 then class='G1';
	  if mod(_n_,3)=2 then class='G2';
	  if mod(_n_,3)=0 then class='Dif';
run;
proc transpose data=t out=t(drop=order _name_);
   var m tv;
   id class;
   by order variable;
run;
data t;
   set t;
      if mod(_n_,2)=0 then variable="";
run;

proc sort data=a;by y size;
run;
proc ttest data=a;
   var volum value ret turn pb;
   class size;
   by y;
   ods output Statistics=t;
run;
data t;
   set t;
      order=int((_n_-1)/3);
      tv="("||left(round(mean/stderr,0.01)||")");
      if abs(mean/stderr)>2.58 then m=round(mean,0.001)||"***";
      else if abs(mean/stderr)>1.96 then m=round(mean,0.001)||"**";
      else if abs(mean/stderr)>1.65 then m=round(mean,0.001)||"*";
      else m=round(mean,0.001)||"";
      if mod(_n_,3)=1 then class='G1';
	  if mod(_n_,3)=2 then class='G2';
	  if mod(_n_,3)=0 then class='Dif';
run;
proc transpose data=t out=t(drop=order _name_);
   var m tv;
   id class;
   by y order variable;
run;
data t;
   set t;
      if mod(_n_,2)=0 then variable="";
run;

%macro ttest(variable,bit,class, year,infile,outfile);
   proc sort data=&infile; by &year  &class;
   run;
   proc ttest data=&infile;
      var &variable;
      class &class;
      by &year;
      ods output Statistics=t;
   run;
   data t;
      set t;
         order=int((_n_-1)/3);
         tv="("||left(round(mean/stderr,0.01)||")");
         if abs(mean/stderr)>2.58 then m=round(mean,10**(-1*&bit))||"***";
         else if abs(mean/stderr)>1.96 then m=round(mean,10**(-1*&bit))||"**";
         else if abs(mean/stderr)>1.65 then m=round(mean,10**(-1*&bit))||"*"; 
         else m=round(mean,10**(-1*&bit))||"";
		 if mod(_n_,3)=1 then class='G1';
	     if mod(_n_,3)=2 then class='G2';
	     if mod(_n_,3)=0 then class='Dif';
   run;
   proc transpose data=t out=t(drop=order _name_);
      var m tv;
      id class;
      by &year order variable;
   run;
   data &outfile;
      set t;
         if mod(_n_,2)=0 then variable="";
   run;
%mend;

%ttest(volum value ret turn pb,4,size,y,a,b);
%ttest(volum value ret turn pb,2,size, ,a,c);
